plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'me.agaman'
version '0.2-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
    compile 'io.github.microutils:kotlin-logging:1.5.4'
    compile 'org.http4k:http4k-core:3.28.0'
    compile 'org.http4k:http4k-client-apache:3.28.0'
    compile 'org.http4k:http4k-client-websocket:3.28.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.github.shyiko.skedule:skedule:0.3.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


def pomConfig = {
    licenses {
        license {
            name "GNU GENERAL PUBLIC LICENSE, Version 3"
            url "https://www.gnu.org/licenses/gpl-3.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "agaman"
            name "Adrián García"
            email "dev@agaman.me"
        }
    }

    scm {
        url "https://github.com/kravent/slackk"
    }
}

publishing {
    publications {
        SlackkMavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'me.agaman'
            artifactId 'slackk'
            version '0.2-SNAPSHOT'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Slack API implementation in Kotlin')
                root.appendNode('name', 'Slackk')
                root.appendNode('url', 'https://github.com/kravent/slackk')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['SlackkMavenPublication']

    pkg {
        repo = 'slackk'
        name = 'slackk'
        userOrg = 'kravent'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/kravent/slackk'
        version {
            name = '0.2-SNAPSHOT'
            released  = new Date()
        }
    }

}
